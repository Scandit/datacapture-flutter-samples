def custom_config_scripts = [
        "$project.projectDir.path/frameworks.gradle",
        "$project.projectDir.path/../../../../../../common/frameworks.gradle"
]

allprojects {
    def customConfigApplied = false
    custom_config_scripts.each { location ->
        def file = file(location)
        if (file.exists()) {
            apply from: file
            repositories repos
            customConfigApplied = true
            return
        }
    }

    if (!customConfigApplied) {
        repositories {
            mavenCentral()
            google()
            maven { url 'https://jitpack.io' }
        }
    }

    configurations.configureEach {
        if (project.hasProperty("snapshotCacheDuration")) {
            resolutionStrategy.cacheChangingModulesFor project.property("snapshotCacheDuration"), 'seconds'
        }
    }

    // Configure JVM targets consistently
    afterEvaluate {
        if (project.hasProperty('android')) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

